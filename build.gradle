plugins {
    id "com.gradle.plugin-publish" version "1.0.0"
    id 'jacoco'
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'codenarc'

group = 'com.github.prokod'
version = '0.17.0-SNAPSHOT'

repositories {
    jcenter()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

apply from: 'codecov.gradle'

//Conditional Signing
ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

dependencies {
    testImplementation gradleTestKit()
    testImplementation ('org.spockframework:spock-core:2.1-groovy-2.5') {
        exclude module: 'groovy'
    }
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.xmlunit:xmlunit-core:2.6.3'
    testImplementation 'org.xmlunit:xmlunit-matchers:2.6.3'
}

test {
    useJUnitPlatform()
    systemProperty 'spock.configuration', rootProject.file('config/spock/SpockConfig.groovy').absolutePath

//    if (project.hasProperty('testTargetCompatibility'))
//        project.property('testTargetCompatibility') == '11' ? exclude('**/LegacyTest.class') : exclude('')
//    else {
//
//    }
}

gradlePlugin {
    plugins {
        crossBuildPlugin {
            id = 'com.github.prokod.gradle-crossbuild'
            displayName = 'Gradle cross build Scala plugin'
            description = 'Adds cross building functionality to Gradle for Scala based projects'
            implementationClass = 'com.github.prokod.gradle.crossbuild.CrossBuildPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/prokod/gradle-crossbuild-scala'
    vcsUrl = 'https://github.com/prokod/gradle-crossbuild-scala'
    tags = ['cross-build-scala-plugins', 'cross-compile', 'scala', 'cross-building']
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Gradle cross build (Scala) plugin'
                description = 'Gradle cross build (Scala) plugin.'
                url = 'https://github.com/prokod/gradle-crossbuild-scala'
                inceptionYear = '2016'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'noamasor'
                        name = 'Noam Asor'
                        email = 'noamas@gmail.com'
                    }
                }
                scm {
                    developerConnection = 'git@github.com:prokod/gradle-crossbuild-scala.git'
                    connection = 'git://github.com/prokod/gradle-crossbuild-scala.git'
                    url = 'https://github.com/prokod/gradle-crossbuild-scala'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def required = { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    if (required) {
        sign publishing.publications.pluginMaven
    }
}

codenarc {
    toolVersion = "1.1"
}

codenarcMain {
    configFile = rootProject.file("config/codenarc/CodenarcMain.groovy")
}

codenarcTest.enabled = false

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        lifecycle {
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
            showExceptions true
            showCauses true
            showStackTraces true
        }
        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                    TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat
    }
    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def startItem = '|  ', endItem = '  |'
            def repeatLength = startItem.length() + output.length() + endItem.length()
            println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
    }
}